apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'


description = 'A New HTML Template Engine'
group = 'com.eiryu'
version = '0.1.0'
archivesBaseName = 'templa'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

[compileJava, compileTestJava, compileGroovy, compileTestGroovy].each {
    it.options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.3'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Templa'
                packaging 'jar'
                description project.description
                url 'https://github.com/eiryu/templa'

                scm {
                    connection 'scm:git:https://github.com/eiryu/templa.git'
                    developerConnection 'scm:git:https://github.com/eiryu/templa.git'
                    url 'https://github.com/eiryu/templa.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'eiryu'
                        name 'Hidetaka Koda'
                        email 'eiryu.co@gmail.com'
                    }
                }
            }
        }
    }
}